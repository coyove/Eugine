(def ThreadManager() => {
    def this.getNextPostId() => sync {
        var id = num(File.read(counterPath))
        file.write(counterPath, str(id++))
        id
    }

    def this.delete(id) => sync if id {
        var path = dataPath + math.floor(id / 1000) + "/" + id
        if file.exists(path) {
            var t = Reply(id)
            var raw = file.read(path, "l")
            t.from(raw[0])
            t.state |= ThreadState.DELETED
            raw[0] = t.toString2()

            if t.reserved != "x" {
                var fn = t.reserved
                t.reserved = "x"
                file.delete(imagesPath + fn)
                file.delete(thumbsPath + fn)
            }

            file.write(path, raw)
            file.delete(path + ".html")

            if t.parent != 0 {
                path = dataPath + math.floor(t.parent / 1000) + "/" + t.parent
                raw = file.read(path, "l")

                for raw do (line, idx) => {
                    var parts = line.split(" ")
                    if parts[0] == str(t.id) {
                        del(raw, idx)
                        break
                    }
                }

                file.write(path, raw)
                file.delete(path + ".html")
            }
        }
    }

    def this.invertState(id, state) => sync if id {
        var path = dataPath + math.floor(id / 1000) + "/" + id
        if file.exists(path) {
            var t = Reply(id)
            var raw = file.read(path, "l")
            t.from(raw[0])
            t.state |= state
            raw[0] = t.toString2()
            file.write(path, raw)
            file.delete(path + ".html")
        }
    }

    def this.deleteImage(id, uid) => sync if id {
        var path = dataPath + math.floor(id / 1000) + "/" + id
        if File.exists(path) {
            var t = Reply(id)
            var raw = file.read(path, "l")
            t.from(raw[0])

            if t.uid == uid && t.reserved != "x" {
                var fn = t.reserved
                t.reserved = "x"
                raw[0] = t.toString2()
                file.write(path, raw)
                file.delete(path + ".html")
                file.delete(imagesPath + fn)
                file.delete(thumbsPath + fn)
            }
        }
    }
})()

(def ThreadState() => {
    var this.NORMAL  = 1
    var this.DELETED = 2
    var this.SAGED   = 4
    var this.LOCKED  = 8
    var this.STICKY  = 16
})()

def struct Thread(raw) => {
    var lines = raw.split("\n")
    if len(lines) == 0 exit(9902)

    var this.main = Reply(0)
    this.main.from(lines[0])

    var this.replies = []
    for lines.tail do (line) => {
        this.replies <+ Reply(0).from(line)

    }

    this.main.children = len(this.replies)
}

def struct ThreadInTimeline(lines) => {
    var this.main = Reply(0)
    this.main.from(lines[0])
    this.main.children = len(lines) - 1

    var this.replies = []
    var this.omitted = 0

    if len(lines) < Config.repliesDisplayedInTimeline {
        for lines.tail do (line) => 
            this.replies <+ Reply(0).from(line)
    } else {
        this.omitted = len(lines) - Config.repliesDisplayedInTimeline
        for (this.omitted),len(lines) do (i) =>
            this.replies <+ Reply(0).from(lines[i])
    }
}

def struct Reply(id) => {
    var this.id = id
    var this.parent = 0
    var this.subject = ""
    var this.name = ""
    var this.comment = ""
    var this.uid = ""
    var this.state = ThreadState.NORMAL
    var this.ts = Time.unix(0)
    var this.ip = ""
    var this.children = 0 
    var this.reserved = "x" // this column has been changed to holding the image path

    def this.from (text) => {
        var parts = text.split(" ")
        // id, parent, name, subject, comment, uid, state, ts, ip, children, reserved
        if len(parts) < 11 exit(9901)

        var this.id       = num(parts[0])
        var this.parent   = num(parts[1])
        var this.subject  = Util.decodeURI(parts[2])
        var this.name     = Util.decodeURI(parts[3])
        var this.comment  = Util.decodeURI(parts[4])
        var this.uid      = parts[5]
        var this.state    = num(parts[6])
        var this.ts       = num(parts[7])
        var this.ip       = parts[8]
        var this.children = num(parts[9])
        var this.reserved = parts[10]

        this
    }

    // this function is ONLY called when a new reply is posted
    // if from() was called before, then calling toString() is forbidden
    def this.toString () => {
        str(this.id) + " " + this.parent + " " + 
        Util.filterString(this.subject) + " " + 
        Util.filterName(this.name) + " " + 
        Util.filterComment(this.comment) + " " + 
        this.uid + " " + this.state + " " + 
        this.ts + " " + this.ip + " " + this.children + " " + this.reserved
    }

    def this.toString2 () => {
        str(this.id) + " " + this.parent + " " + 
        Util.encodeURI(this.subject) + " " + 
        Util.encodeURI(this.name) + " " + 
        Util.encodeURI(this.comment) + " " + 
        this.uid + " " + this.state + " " + 
        this.ts + " " + this.ip + " " + this.children + " " + this.reserved
    }
}

