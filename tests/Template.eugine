(def Template() => {
    def header(title) => @"<!DOCTYPE html>
<html>
<head>
    <meta charset='utf-8'>
    <meta name='referrer' content='origin'>
    <meta name='viewport' content='width=device-width, initial-scale=1.0'>
    <link href='data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAACAAAAAgBAMAAACBVGfHAAAAKlBMVEVLWX3///9seJVyfZlaZ4heaopUYYS8wc7b3uVjb47x8vXT1t96hJ/8/Pwv3cUDAAAAOklEQVR4XmOgH5hsDAZmcIFGQTAQxy2wEV3AxcVHUDA0NIABDhiYBAUZGBhGrADnRSm0cGZLYKAeAABwbwqMNFT1HQAAAABJRU5ErkJggg==' rel='icon' type='image/x-icon'>
    <title>" + title + " | " + Config.siteName + @"</title>
</head>
<body>
    <div id='container'>"

    def footer() => @"
    </div>
</body>"

    def _post(action, fields) => {
        var ret = @"
<form action='" + action + @"' method='post'>
    <table>"
        for fields do (f) => 
            ret <+ ("<tr><td>" + f.text + "</td><td>" + ({
                text: 
                    "<input type='text' name='" + f.name + "'/>",
                textarea: 
                    "<textarea name='" + f.name + "' rows='" + f.rows + "'></textarea>",
                checkbox:
                    "<input type='checkbox' name='" + f.name + "'/><label for='" + f.name + "'>" + f.label + "</label>",
                submit:
                    "<input type='submit' value='" + f.value + "'/>"
            })[f.type] + "</td></tr>")

        ret + @"
    </table>
</form>"
    }

    def post(endpoint, button) => {
        _post(endpoint, [
            { text: "标题", type: "text", name: "subject" },
            { text: "名称", type: "text", name: "name" },
            { text: "正文", type: "textarea", name: "comment", rows: 10 },
            { text: "", type: "submit", value: button },
        ])
    }

    def this.index() => {
        var ret = header("首页")
        ret <+ post("/new/0", "提交")
        ret <+ footer()
        ret
    }

    def _thread(t) => @ret("") {
        ret <+ @"
        <div>
            <div><span>" <+ ((if t.subject == "untitled" "" else t.subject) + "</span> <span>" + 
                t.name + "</span> <span>" + 
                t.uid + "</span> <span>" + 
                Time.utcFormat(t.ts) + "</span> <span>No." +
                t.id) <+ @"</span></div>
            <div>" <+ ("" + t.comment) <+ @"</div>
        </div>"
    }

    def this.thread(t) => {
        var ret = header("#" + t.main.id + " " + t.main.subject)
        ret <+ post("/new/" + t.main.id, "回复")
        ret <+ _thread(t.main)

        for t.replies do (reply) => {
            ret <+ "<hr>"
            ret <+ _thread(reply)
        }

        ret <+ footer()
        ret
    }

    def this.error(msg) => {
        var ret = header("错误")
        ret <+ msg
        ret <+ footer()
        ret
    }
})()